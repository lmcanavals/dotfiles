#
# Override stuff
#
setopt vi
#setopt hist_ignore_dups
setopt hist_ignore_all_dups
setopt hist_reduce_blanks

#
# Prompt stuff
#
prompt off

autoload -U colors && colors

# /home/user/folder/stuff/tra/cool --> ~/f/s/t/cool
# cooler thank %~
function cool_folder() {
	print $(echo ${PWD/$HOME/󱂵 } | sed -e "s/\(\w\)\(\w\+\)\//\1\//g")
}
function prompt_color() {
	local c
	[[ EUID -eq 0 ]] && c="red" || c="yellow"
	[[ $KEYMAP == vicmd ]] && c="blue"
	print "%{$fg[$c]%} "
	#print "%{$fg_bold[$c]%} "
}
function left_prompt() {
	local s
	[[ $KEYMAP == vicmd ]] && s=":" || s="_"
	print "$(prompt_color)┏━━━󰧞\n ┗󰧞 %#$s%{$reset_color%} "
}
function right_prompt() {
	local s
	s="$(prompt_color)%n%{$fg[yellow]%}@%{$fg[cyan]%}%m:"
	s+="%{$fg[blue]%}$(cool_folder)%{$reset_color%}"
	print "$s"
}

ZLE_RPROMPT_INDENT=0 # not buggy anymore
function prompt_lmcs_setup() {
	PS2="$(prompt_color)%_┊%{$reset_color%} "
	PS3="$(prompt_color)?#┊%{$reset_color%} "
	PS4="$(prompt_color)+%N:%i:%_┊%{$reset_color%} "

	add-zsh-hook precmd prompt_lmcs_precmd
}
function prompt_lmcs_precmd() {
	PS1="$(left_prompt)"
	if [[ $LCSTARSHIP -eq 0 ]]; then
		RPS1="$(right_prompt)"
	fi
	set_title ${(%):-"%n@%m:$(cool_folder)"}
}
function prompt_lmcs_help() {
	<<__EOF0__
  prompt lmcs

    My prompt it's super cool and uses starship to easily add stuff to the right

        - left: a simple %#_ green for users and red for root
        - right: starship stuff configured at $HOME/.config/starship.toml
__EOF0__
}
function zle-keymap-select() {
	PROMPT="$(left_prompt)"
	() { return $__prompt_status }
	zle reset-prompt
}
function zle-line-init() {
	typeset -g __prompt_status="$?"
}
zle -N zle-keymap-select
zle -N zle-line-init

prompt_themes+=( lmcs )
prompt_themes=( "${(@on)prompt_themes}" )

prompt lmcs

LCSTARSHIP=0
if command -v starship &> /dev/null; then
	eval "$(starship init zsh)"
	LCSTARSHIP=1
fi
export LCSTARSHIP

#
# Shortcuts and aliases
#
abk+=(
	"d"   " GIT_DIR=$HOME/.dotfiles.git GIT_WORK_TREE=$HOME "
	"ga"  " git add ."
	"gc"  " git commit -a -m \"\""
	"gd"  " git difftool"
	"gp"  " git push"
	"gl"  " git pull"
	"gr"  " git restore "
	"grm" " git rm --cached "
	"gs"  " git status -u"
	"j"   "| jq -r '.[].name' "
	"n"   " nvim "
	"p"   " ps -e f"
	"qd"  " pacman -Qdt"
	"qe"  " pacman -Qet"
	"qi"  " pacman -Qi "
	"ql"  " pacman -Ql "
	"qm"  " pacman -Qm"
	"qo"  " pacman -Qo "
	"qs"  " pacman -Qs "
	"rs"  "sudo pacman -Rncs "
	"s"   "sudo pacman -S "
	"sc"  " sudo pacman -Sc"
	"si"  " pacman -Si "
	"ss"  " pacman -Ss "
	"sy"  "sudo pacman -Syu"
	"x"   "| xsel -ib"
)
bind2maps emacs viins       -- -s '^@' zleiab

alias eza="eza -F --icons --colour=auto --group-directories-first "
alias ea="eza -la "
alias eaa="eza -a .*(.)"
alias ead="eza -d .*(/)"
alias ebig="eza -l *(.OL[1,10])"
alias ee="eza -lb "
alias eed="eza -d *(/)"
alias eee="eza -d *(/^F)"
alias enew="eza -rtl *(D.om[1,10])"
alias enewdir="eza -rtdl *(/om[1,10]) .*(D/om[1,10])"
alias eold="eza -rtl *(D.Om[1,10])"
alias eolddir="eza -rtdl *(/Om[1,10]) .*(D/Om[1,10])"
alias esmall="eza -rl *(.oL[1,10])"
alias dotfiles="git --git-dir=$HOME/.dotfiles.git --work-tree=$HOME "

#
# some extra sources
#
[[ -s $ZDOTDIR/dynamic.zsh ]] && source $ZDOTDIR/dynamic.zsh
source <(fzf --zsh)

#
# plugin manager
# TODO I need to document this crap or I will forget forever
#
function lmcs-plugman() {
	local plug_gh_url
	local plug_name
	local oldpwd
	local plug_folder
	for fullname in $lmcs_plugins; do
		plug_name=$(basename $fullname)
		plug_gh_url="https://github.com/$fullname.git"
		plug_folder="$ZDOTDIR/plugins/$plug_name"
		case $@ in
			install)
				if [[ ! -d $ZDOTDIR/plugins/$plug_name ]]; then
					echo "installing $plug_name"
					git clone --depth 1 $plug_gh_url $plug_folder
				else
					echo "$plug_name is already installed, try $0 update" 
				fi
				;;
			load)
				if [[ ! -d $ZDOTDIR/plugins/$plug_name ]]; then
					echo "$plug_name not installed... installing it..."
					git clone --depth 1 $plug_gh_url $plug_folder
				fi
				source $ZDOTDIR/plugins/$plug_name/$plug_name.plugin.zsh
				;;
			update)
				if [[ -d $ZDOTDIR/plugins/$plug_name ]]; then
					echo "updating $plug_name"
					oldpwd=$(pwd)
					cd $ZDOTDIR/plugins/$plug_name
					git pull
					cd $oldpwd
				else
					echo "$plug_name not installed... installing it..."
					git clone --depth 1 $plug_gh_url $plug_folder
				fi
				;;
		esac
	done
}

lmcs_plugins=(
	zsh-users/zsh-autosuggestions
	# zsh syntax highlighting recommends loading it last
	# zsh-users/zsh-syntax-highlighting
	zdharma-continuum/fast-syntax-highlighting
)
lmcs-plugman load

# vim: set ts=2:sw=2:noet:sts=2:
